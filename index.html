<html>

<head>
	<title>地球与月球</title>
	<style type="text/css">
		html {
			width: 100%;
			height: 100%;
		}

		body {
			margin: 5px;
			width: calc(100% - 10px);
			height: calc(100% - 40px);
		}
	</style>
	<script type="text/javascript" src="js/Matrix.js"></script>
	<script type="text/javascript" src="js/MV.js"></script>
	<script type="text/javascript" src="js/shader.js"></script>
	<script type="text/javascript" src="js/sphere.js"></script>
	<script type="text/javascript" src="js/starBox.js"></script>
	<script type="text/javascript" src="js/main.js"></script>
</head>

<body onload="initializer();">
	<div style="display:flex; justify-content:space-between; line-height:20px; color:black; font-size:1vw;">
		<text>X/V-物体沿顺/逆时针方向旋转</text>
		<text>Q/E-光源位置沿逆/顺时针方向移动</text>
		<text>Z/C-拉远/拉近摄像头</text>
		<text>P-切换投影方式</text>
		<text>T-自动旋转与否</text>
		<text>R-重置状态</text>
	</div>
	<canvas height="800" width="1300" id="myCanvas">
		若看到这个文字，说明浏览器不支持WebGL!
	</canvas>
</body>

<script id="earth-vertex" type="x-shader/x-vertex">#version 300 es
precision mediump float;

uniform mat4 transformMatrix;  // MVP的变换矩阵
uniform mat4 modelVertexPosition;
uniform vec3 cameraLocation;
uniform vec3 sunlightLocation;

in vec3 vertexPosition;
in vec2 originalVertexCoordinates;
in vec3 normalVector;

out vec2 vertexTextureCoordinates;
out vec4 vertexAmbient;
out vec4 vertexDiffuse;
out vec4 vertexSpecular;

void calculateVertexLigh(inout vec4 ambient,	inout vec4 diffuse,	inout vec4 specular, in vec3 normal,
                in vec3 lightLocation,	in vec4 lightAmbient, in vec4 lightDiffuse,	in vec4 lightSpecular){
                
	vec3 normalH = normalize((modelVertexPosition * vec4(vertexPosition + normal, 1)).xyz - (modelVertexPosition * vec4(vertexPosition, 1)).xyz);
	vec3 vertex2light = normalize(normalize(lightLocation - (modelVertexPosition * vec4(vertexPosition, 1)).xyz));

	ambient = lightAmbient;

	diffuse = lightDiffuse * max(0.0, dot(normalH, vertex2light));

	specular = lightSpecular * max(0.0,
	    pow(dot(normalH, normalize(vertex2light + normalize(cameraLocation -
	    (modelVertexPosition * vec4(vertexPosition, 1)).xyz))), 9.0));
}

void main()
{
    //计算绘制顶点位置
	gl_Position = transformMatrix * vec4(vertexPosition, 1);
	vec4 originalAmbient, originalDiffuse, originalSpecular;
	calculateVertexLigh(originalAmbient, originalDiffuse, originalSpecular, normalize(normalVector), sunlightLocation, vec4(0.05, 0.05, 0.05, 1.0), vec4(1.0, 1.0, 1.0, 1.0), vec4(0.3, 0.3, 0.3, 1.0));

	vertexAmbient = originalAmbient;
	vertexDiffuse = originalDiffuse;
	vertexSpecular = originalSpecular;

	vertexTextureCoordinates = originalVertexCoordinates;
}
</script>
<script id="earth-fragment" type="x-shader/x-fragment">#version 300 es
precision mediump float;

uniform sampler2D sampleDayTexture;
uniform sampler2D sampleNightTexture;

in vec2 vertexTextureCoordinates;
in vec4 vertexAmbient;
in vec4 vertexDiffuse;
in vec4 vertexSpecular;

out vec4 fragmentTexture;

void main()
{
	vec4 dayTexture;
	vec4 nightTexture;

	dayTexture = texture(sampleDayTexture, vertexTextureCoordinates);
	dayTexture = dayTexture * vertexAmbient + dayTexture * vertexSpecular + dayTexture * vertexDiffuse;
	nightTexture = texture(sampleNightTexture, vertexTextureCoordinates);
	nightTexture = nightTexture * vec4(0.5, 0.5, 0.5, 1.0); 
	
	if (vertexDiffuse.x > 0.25) {
		fragmentTexture = dayTexture;
	} else if (vertexDiffuse.x < 0.05) {
		fragmentTexture = nightTexture;
	} else {
		float ratio = (vertexDiffuse.x - 0.05) / 0.16;
		fragmentTexture = ratio * dayTexture + (1.0 - ratio) * nightTexture;
	}
}

</script>

<script id="moon-vertex" type="x-shader/x-vertex">#version 300 es
uniform mat4 transformMatrix;  // MVP的变换矩阵
uniform mat4 modelVertexPosition;
uniform vec3 cameraLocation;
uniform vec3 sunlightLocation;

in vec3 vertexPosition;
in vec2 originalVertexCoordinates;
in vec3 normalVector;

out vec2 vertexTextureCoordinates;
out vec4 finalTextureLight;

vec4 calculateVertexLigh(in vec3 normal,	in vec3 lightLocation, in vec4 lightAmbient, in vec4 lightDiffuse, in vec4 lightSpecular){
	vec3 normalH = (modelVertexPosition * vec4(vertexPosition + normal, 1)).xyz - (modelVertexPosition * vec4(vertexPosition, 1)).xyz;
	vec3 vertex2light = normalize(normalize(lightLocation - (modelVertexPosition * vec4(vertexPosition, 1)).xyz)); //格式化vp

	vec4 ambient = lightAmbient;
	vec4 diffuse = lightDiffuse * max(0.0, dot(normalize(normalH), vertex2light));
	vec4 specular = lightSpecular * max(0.0, pow(dot(normalize(normalH),
        normalize(vertex2light + normalize(cameraLocation - (modelVertexPosition * vec4(vertexPosition, 1)).xyz))),
        6.0));

	return (ambient + diffuse + specular);
}

void main()
{
	gl_Position = transformMatrix * vec4(vertexPosition, 1);
	finalTextureLight = calculateVertexLigh(normalize(normalVector), sunlightLocation, vec4(0.05, 0.05, 0.025, 1.0), vec4(1.0, 1.0, 0.5, 1.0), vec4(0.3, 0.3, 0.15, 1.0));

	vertexTextureCoordinates = originalVertexCoordinates;
}


</script>
<script id="moon-fragment" type="x-shader/x-fragment">#version 300 es
precision mediump float;

uniform sampler2D sampleTexture;

in vec2 vertexTextureCoordinates;
in vec4 finalTextureLight;
out vec4 fragmentTexture;
void main() {
    vec4 finalColor = texture(sampleTexture, vertexTextureCoordinates);

    fragmentTexture = finalColor * finalTextureLight;
}
</script>

<script id="starbox-vertex" type="x-shader/x-vertex">
uniform mat4 transformMatrix;  // MVP的变换矩阵

attribute vec3 vertexPosition;
attribute vec2 originalVertexCoordinates;

varying vec2 vertexTextureCoordinates; //传递变量
void main() {
    gl_Position = transformMatrix * vec4(vertexPosition, 1);
    vertexTextureCoordinates = originalVertexCoordinates;
}
</script>
<script id="starbox-fragment" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D sampleTexture;

varying vec2 vertexTextureCoordinates;  //varying不可使用 es 300
void main()
{
	gl_FragColor = texture2D(sampleTexture, vertexTextureCoordinates);
}

</script>